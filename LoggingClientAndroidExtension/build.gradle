/*
 *     Copyright (C) 2016 Mobile Interactive Systems Research Group
 *
 *     This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU Lesser General Public License as published by
 *     the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU Lesser General Public License for more details.
 *
 *     You should have received a copy of the GNU Lesser General Public License
 *     along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName VERSION
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'at.fhhagenberg.mint.automate:LoggingClientJavaCore:1.0.0'
    compile 'org.apache.thrift:libthrift:0.9.3'
    compile 'org.apache.logging.log4j:log4j-core:2.5'
    compile 'org.slf4j:slf4j-android:1.7.19'
    compile 'com.android.support:support-v4:23.4.0'
}

apply plugin: 'maven-publish'

publishing {
    publications {
        android.buildTypes.all { variant ->
            "${variant.name}Aar"(MavenPublication) {

                def libVersion = android.defaultConfig.versionName
                def artifactName = project.getName()

                groupId GROUP_ID
                artifactId artifactName
                version libVersion + (variant.name == 'debug' ? '-SNAPSHOT' : '')

                artifact("${project.buildDir}/outputs/aar/${project.getName()}-${variant.name}.aar")

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    def configurationNames = ["${variant.name}Compile", 'compile']

                    configurationNames.each { configurationName ->
                        configurations[configurationName].allDependencies.each {
                            if (it.group != null && it.name != null) {
                                def dependencyNode = dependenciesNode.appendNode('dependency')
                                dependencyNode.appendNode('groupId', it.group)
                                dependencyNode.appendNode('artifactId', it.name)
                                dependencyNode.appendNode('version', it.version)

                                if (it.excludeRules.size() > 0) {
                                    def exclusionsNode = dependencyNode.appendNode('exclusions')
                                    it.exclusionRules.each { rule ->
                                        def exclusionNode = exclusionsNode.appendNode('exclusion')
                                        exclusionNode.appendNode('groupId', rule.group)
                                        exclusionNode.appendNode('artifactId', rule.module)
                                    }
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = BINTRAY_USER
    key = BINTRAY_API_KEY
    pkg {
        repo = 'automate'
        name = ARTIFACT_ID
        userOrg = 'mint-hagenberg'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/mint-hagenberg/automate-' + ARTIFACT_ID
        publications = ['releaseAar']
        version {
            name = VERSION
            released = new Date()
            vcsTag = VERSION
        }
    }
}

android.buildTypes.all { variant ->
    def name = variant.name.substring(0, 1).toUpperCase() + variant.name.substring(1)
    task "publish${name}Local"(dependsOn: "publish${name}AarPublicationToMavenLocal")
}